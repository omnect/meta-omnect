require conf/machine/genericx86-64.conf

# additional kernel config
OMNECT_KERNEL_SRC_URI:append = " \
    file://crc32c-intel_module.cfg \
    file://disable_wlan.cfg \
    file://intel-lpc_ich_module.cfg \
    file://intel-mei_module.cfg \
    file://intel-vbtn_module.cfg \
    file://pc-speaker.cfg \
    file://tcg_tpm_2.0.cfg \
"
# default kernel parameter
APPEND = "console=tty0"
# module autoload
KERNEL_MODULE_AUTOLOAD = "mei"

# bootloader
PREFERRED_PROVIDER_virtual/bootloader = "grub-efi"
# don't create grub-efi in rootfs
EFI_PROVIDER = " "

# swupdate has a hard dependency to libubootenv (in swupdate_vars.(c|h)).
# so we can not get rid of it, but we can get rid of a uboot boot script.
DEPENDS:remove:pn-libubootenv = "u-boot-default-script"

# initramfs type
OMNECT_INITRAMFS_FSTYPE = "cpio.gz"

# image generation
WKS_FILE = "omnect-os.grub.wks.in"
# no differentiation for now between release an devel images concerning rootfs
# size
OMNECT_PART_SIZE_ROOTFS ?= "${@bb.utils.contains('OMNECT_RELEASE_IMAGE', '1', '819200', '819200', d)}"
# where to find initial grubenv
WICVARS:append = " OMNECT_GRUBENV_FILE"
OMNECT_GRUBENV_FILE = "${LAYERDIR_omnect}/files/grubenv"
OMNECT_ROOT_DEVICE = "/dev/nvme0n1"

# adu compatibility id
OMNECT_ADU_DEVICEPROPERTIES_COMPATIBILITY_ID = "0"

# machine
MACHINE_FEATURES = " \
    3g \
    acpi \
    efi \
    grub \
    keyboard \
    pcbios \
    pci \
    screen \
    serial \
    tpm2 \
    usbhost \
    x86 \
"
MACHINEOVERRIDES = "omnect_grub:genericx86-64"
MACHINE_EXTRA_RRECOMMENDS += "linux-firmware-i915"
# don't install every available firmware
MACHINE_EXTRA_RRECOMMENDS:remove = "linux-firmware"

# if an LTE modem is present it is a Quectel EC25
OMNECT_WAIT_ONLINE_INTERFACES_BUILD =  "enp1s0:enp2s0:enp3s0:wwp0s21f0u8i4"

# configure hardware watchdog
# the maximum watchdog deadline depends on the hardware capabilities
SYSTEMD_RuntimeWatchdogSec  = "60"
SYSTEMD_RebootWatchdogSec   = "60"
