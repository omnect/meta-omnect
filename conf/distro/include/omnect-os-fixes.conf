# busybox: enforce identical busybox configuration for iot and iotedge; non-busybox tools lspci, lsusb, ps, nsenter are used
BBMASK:append = " ${@bb.utils.contains('DISTRO_FEATURES', 'iotedge', 'meta-virtualization/recipes-core/busybox/busybox_%.bbappend', '', d)}"

# ca-certificates:
# update-ca-certificates is not working when we include the following patch
# which poky applies
SRC_URI:remove:pn-ca-certificates = "file://update-ca-certificates-support-Toybox.patch"

# dbus: correct build dependency, otherwise 'autoconf-archive' lands in sbom
DEPENDS:remove:pn-dbus = "autoconf-archive"
DEPENDS:append:pn-dbus = " autoconf-archive-native"

# docker: we don't use btrfs and support is disabled in docker-moby per default anyway
DEPENDS:remove:pn-docker-moby = "btrfs-tools"

# e2fsprogs: correct build dependency, otherwise 'autoconf-archive' lands in sbom
DEPENDS:remove:pn-e2fsprogs = "autoconf-archive"
DEPENDS:append:pn-e2fsprogs = " autoconf-archive-native"

# gobject-introspection: correct build dependency, otherwise 'autoconf-archive' lands in sbom
DEPENDS:remove:pn-gobject-introspection = "autoconf-archive"
DEPENDS:append:pn-gobject-introspection = " autoconf-archive-native"

# icu: correct build dependency, otherwise 'autoconf-archive' lands in sbom
DEPENDS:remove:pn-icu = "autoconf-archive"
DEPENDS:append:pn-icu = " autoconf-archive-native"

# libx11: correct build dependency, otherwise 'autoconf-archive' lands in sbom
DEPENDS:remove:pn-libx11 = "autoconf-archive"
DEPENDS:append:pn-libx11 = " autoconf-archive-native"

# lshw: fix lshw e.g. on tauri-l
FILESEXTRAPATHS:prepend:pn-lshw := "${LAYERDIR_omnect}/files/patches:"
SRC_URI:append:pn-lshw = " file://lshw_github_PR87.patch"

# networkmanager: we removed qemu-usermode from MACHINE_FEATURES, this results
# in failure of networkmanager configuration step for gobject-introspection,
# unless we build without vala support
PACKAGECONFIG:remove:pn-networkmanager = "vala"
