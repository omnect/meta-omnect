# - we enable overlayfs for all kernels (dependency of our partition layout)
# - we disable kvm (e.g. set by linux-phytec-imx)
OMNECT_KERNEL_SRC_URI_LTE = " \
    file://mbim.cfg \
    file://qualcomm.cfg \
    file://sierra.cfg \
"

OMNECT_KERNEL_SRC_URI = " \
    file://cpu_freq_default_gov_schedutil.cfg \
    file://enable-overlayfs.cfg \
    file://enable-cifs.cfg \
    file://enable-ext4-fs-security.cfg \
    file://disable-kvm.cfg \
    file://netfilter.cfg \
    file://usb-serial-devices.cfg \
    file://virtualization.cfg \
    file://wireguard.cfg \
    ${@bb.utils.contains('MACHINE_FEATURES', 'pstore', 'file://pstore.cfg', '', d)} \
    ${@bb.utils.contains('MACHINE_FEATURES', '3g', '${OMNECT_KERNEL_SRC_URI_LTE}', '', d)} \
    ${@bb.utils.contains('MACHINE_FEATURES', 'can', 'file://vcan_vxcan.cfg', '', d)} \
"

KERNEL_FEATURES:append = " \
    cfg/docker.scc \
    cfg/lxc.scc \
    cfg/kubernetes.scc \
"

# enforce identical kernel config independent of meta-virtualization
BBMASK:append = " meta-virtualization/recipes-kernel/linux/linux-%.bbappend"
FILESEXTRAPATHS:prepend:pn-linux-phytec-imx := "${LAYERDIR_omnect}/recipes-kernel/linux/files:"
FILESEXTRAPATHS:prepend:pn-linux-raspberrypi := "${LAYERDIR_omnect}/recipes-kernel/linux/files:${LAYERDIR_virtualization-layer}/recipes-kernel/linux/linux-yocto:"
FILESEXTRAPATHS:prepend:pn-linux-yocto := "${LAYERDIR_omnect}/recipes-kernel/linux/files:${LAYERDIR_virtualization-layer}/recipes-kernel/linux/linux-yocto:"

SRC_URI:append:pn-linux-phytec-imx = " ${OMNECT_KERNEL_SRC_URI}"
SRC_URI:append:pn-linux-raspberrypi = " ${OMNECT_KERNEL_SRC_URI}"
SRC_URI:append:pn-linux-yocto = " ${OMNECT_KERNEL_SRC_URI}"
