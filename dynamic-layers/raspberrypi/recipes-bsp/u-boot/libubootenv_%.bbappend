DEPENDS += "u-boot-default-script"
DEPENDS += "bc-native"

# multiply with 1024; convert to hex
ics_dm_conv_param() {
	local param="$1"
	param="$(echo "${param} * 1024" | bc)"
	param="0x$(printf '%x' ${param})"
	echo ${param}
}

# generate fw_env.config based on on distro configuration
do_install_append() {
	local  blk_dev="/dev/mmcblk0"
	local env_size=$(ics_dm_conv_param ${ICS_DM_PART_SIZE_UBOOT_ENV})
	local  offset1=$(ics_dm_conv_param ${ICS_DM_PART_OFFSET_UBOOT_ENV1})
	local  offset2=$(ics_dm_conv_param ${ICS_DM_PART_OFFSET_UBOOT_ENV2})
	rm -rf ${D}${sysconfdir}/fw_env.config
	echo "# Generated by ${PN} bbappend"      >${WORKDIR}/fw_env.config
	echo "${blk_dev} ${offset1} ${env_size}" >>${WORKDIR}/fw_env.config
	echo "${blk_dev} ${offset2} ${env_size}" >>${WORKDIR}/fw_env.config
	install -m 0644 -D ${WORKDIR}/fw_env.config ${D}${sysconfdir}/fw_env.config
}
